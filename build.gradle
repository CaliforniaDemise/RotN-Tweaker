import org.jetbrains.gradle.ext.Gradle

plugins {
    id 'java'
    id 'java-library'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.1.7'
    id 'com.gtnewhorizons.retrofuturagradle' version '1.3.33'
}

version = "0.0.1"
group = "com.rebirthofthenight.rotntweaker" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "RotN-Tweaker"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

sourceSets {
    // Classes outside RotN Tweaker to replace
    replacement {
        java {
            compileClasspath += main.compileClasspath
            runtimeClasspath += main.runtimeClasspath
        }
    }
    main {
        java {
            compileClasspath += replacement.output
            runtimeClasspath += replacement.output
        }
    }
}

minecraft {
    mcVersion = '1.12.2'
    useDependencyAccessTransformers = true
}

repositories {
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        url "https://maven.blamejared.com"
    }
    mavenLocal()
}

dependencies {
    implementation files("./libs/GlareTorch-Ver.8.11.jar", "./libs/hooklib-2.12.jar")

    implementation rfg.deobf("CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-4.1.20.698")

    implementation rfg.deobf("curse.maven:rustic-256141:3107974")

    implementation rfg.deobf("curse.maven:primalcore-247907:2734701")

    implementation rfg.deobf("curse.maven:totem-expansion-291724:2599101")

    implementation rfg.deobf("curse.maven:quark-rotn-edition-417392:4641362")
    implementation rfg.deobf("vazkii.autoreglib:AutoRegLib:1.3-32.33")

    implementation rfg.deobf("curse.maven:bwm-suite-246760:3289033")
    implementation "betterwithmods.core:BetterWithLib:1.12-1.5:dev"

    implementation rfg.deobf("curse.maven:pyrotech-306676:4798313")
    implementation rfg.deobf("curse.maven:athenaeum-284350:4633750")
    implementation rfg.deobf("curse.maven:dropt-284973:3758733")
    implementation rfg.deobf("curse.maven:patchouli-306770:3162874")
    // implementation 'albedo:albedo:1.12.2:1.1.0'
}

processResources {
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version

    filesMatching(['mcmod.info', 'pack.mcmeta']) { fcd ->
        fcd.expand (
                'version': project.version,
                'mcversion': project.minecraft.version
        )
    }
}


idea {
    module {
        inheritOutputDirs = true
    }
    project {
        settings {
            runConfigurations {
                "1. Run Client"(Gradle) {
                    taskNames = ["runClient"]
                }
                "2. Run Server"(Gradle) {
                    taskNames = ["runServer"]
                }
                "3. Run Obfuscated Client"(Gradle) {
                    taskNames = ["runObfClient"]
                }
                "4. Run Obfuscated Server"(Gradle) {
                    taskNames = ["runObfServer"]
                }
            }
            compiler.javac {
                afterEvaluate {
                    javacAdditionalOptions = "-encoding utf8"
                    moduleJavacAdditionalOptions = [
                            (project.name + ".main"): tasks.compileJava.options.compilerArgs.collect { '"' + it + '"' }.join(' ')
                    ]
                }
            }
        }
    }
}
